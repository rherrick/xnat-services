buildscript {
    ext {
        vXnat = "1.7.5-SNAPSHOT"
    }
    repositories {
        mavenLocal()
        jcenter()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        maven {
            url "https://nrgxnat.jfrog.io/nrgxnat/libs-release"
            name "XNAT Release Repository"
        }
        maven {
            url "https://nrgxnat.jfrog.io/nrgxnat/libs-snapshot"
            name "XNAT Snapshot Repository"
        }
    }
    dependencies {
        classpath "com.github.kongchen:swagger-maven-plugin:3.1.+"
        classpath "com.netflix.nebula:gradle-ospackage-plugin:4.5.1"
        classpath "gradle.plugin.com.github.zhurlik:gradle-swagger-plugin:0.2"
        classpath "gradle.plugin.com.palantir.gradle.gitversion:gradle-git-version:0.8.0"
        classpath "io.franzbecker:gradle-lombok:1.10"
        classpath "io.spring.gradle:dependency-management-plugin:1.0.3.RELEASE"
        classpath "io.swagger:swagger-core:1.5.+"
    }
}

plugins {
    id "application"
    id "com.github.zhurlik.swagger" version "0.2"
    id "com.palantir.git-version" version "0.10.0"
    id "eclipse"
    id "groovy"
    id "idea"
    id "io.franzbecker.gradle-lombok" version "1.11"
    id "io.spring.dependency-management" version "1.0.4.RELEASE"
    id "jacoco"
    id "java"
    id "maven"
    id "maven-publish"
    id "nebula.deb" version "4.5.1"
    id "war"
}

group "org.nrg.xnat"
version "2.0-SNAPSHOT"

def fullName = "xnat-web"
def vSpring = "4.3.13.RELEASE"
def vJackson = "2.9.3"
def vCommonsLang = "3.7"
def vTomcat70 = "7.0.82"
def vTomcat80 = "8.0.48"
def vTomcat85 = "8.5.24"
def vTomcat90 = "9.0.2"
def vTomcat = vTomcat70
def vSlf4j = "1.7.25"
def vLog4j2 = "2.9.1"
def vServletApi = "3.1.0"
def vJstl = "1.2"
def vJUnit = "1.0.2"

sourceCompatibility = 1.8
targetCompatibility = 1.8

mainClassName = "org.nrg.xnat.web.application.XnatApplication"
applicationName = "xnat-start"

distributions {
    main {
        baseName = fullName
    }
}

task addedStartScripts(type: CreateStartScripts) {
    mainClassName = "org.nrg.xnat.web.application.XnatShutdown"
    applicationName = "xnat-shutdown"
    outputDir = new File(project.buildDir, "scripts")
    classpath = jar.outputs.files + project.configurations.runtimeClasspath
}

task createDeb(type: Deb) {
    packageName = fullName
    version = project.version
    release = 1

    configurationFile("/etc/defaults/xnat")
    // installUtils file("scripts/deb/utils.sh")
    // preInstall file("scripts/deb/preInstall.sh")
    // postInstall file("scripts/deb/postInstall.sh")
    // preUninstall file("scripts/deb/preUninstall.sh")
    // postUninstall file("scripts/deb/postUninstall.sh")

    requires "postgresql", "9.5"

    into "/var/lib/xnat"

    from(jar.outputs.files) {
        into "lib"
    }
    from(configurations.runtimeClasspath) {
        into "lib"
    }
    from("src/main/scripts") {
        into "bin"
        // exclude "database"
        fileMode 0550
    }
    from("src/main/resources") {
        fileType CONFIG.set(NOREPLACE.flag())
        into "conf"
    }
    from("home") {
        createDirectoryEntry = true
        fileMode 0500
        into "home"
    }
    // from("endorsed") {
    //     into "/usr/share/tomcat/endorsed"
    // }

    link("/etc/init.d/xnat", "/var/lib/xnat/bin/xnat-start")

    customField "Build-Host", "http://mycihost"
    customField([
            "Commit-ID": "deadbeef",
            "Owner"    : "John Doe <johndoe@sweetdomain.io>"
    ])
    customFields << [
            "Build-Job": "XnatProject"
    ]
}

project.getTasksByName(ApplicationPlugin.TASK_START_SCRIPTS_NAME, true).each { task -> task.dependsOn(addedStartScripts) }

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

dependencies {
    implementation "org.springframework:spring-webmvc:${vSpring}"
    implementation "com.fasterxml.jackson.core:jackson-annotations:${vJackson}"
    implementation "com.fasterxml.jackson.core:jackson-core:${vJackson}"
    implementation "com.fasterxml.jackson.core:jackson-databind:${vJackson}"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.9.3"
    implementation "org.apache.commons:commons-lang3:${vCommonsLang}"
    implementation "javax.servlet:jstl:${vJstl}"

    implementation "org.slf4j:jcl-over-slf4j:${vSlf4j}"
    implementation "org.apache.logging.log4j:log4j-api:${vLog4j2}"
    implementation "org.apache.logging.log4j:log4j-core:${vLog4j2}"
    implementation "org.apache.logging.log4j:log4j-slf4j-impl:${vLog4j2}"

    implementation "org.apache.tomcat.embed:tomcat-embed-core:${vTomcat}"
    implementation "org.apache.tomcat.embed:tomcat-embed-logging-juli:${vTomcat}"
    implementation "org.apache.tomcat.embed:tomcat-embed-jasper:${vTomcat}"
    implementation "org.apache.tomcat:tomcat-jasper:${vTomcat}"
    implementation "org.apache.tomcat:tomcat-jasper-el:${vTomcat}"
    implementation "org.apache.tomcat:tomcat-jsp-api:${vTomcat}"

    providedCompile "javax.servlet:javax.servlet-api:${vServletApi}"

    testImplementation "org.junit.platform:junit-platform-commons:${vJUnit}"
}
